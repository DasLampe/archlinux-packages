# Maintainer; Eugene Lamskoy <e.lamskoy@gmail.com>
_pecl=PDO_DBLIB
_ext=pdo_dblib
pkgname="php-$_ext"
pkgver=1.0
pkgrel=3
pkgdesc="pdo_dblib extension for PHP"
arch=('i686' 'x86_64')
url="http://pecl.php.net/package/$_pecl"
license=('PHP')
depends=('php' 'freetds')
source=(
    http://pecl.php.net/get/$_pecl-$pkgver.tgz
    freetds.patch
    php-54.patch
)
install="php-$_ext.install"

build() {
	cd "$srcdir/$_pecl-$pkgver"
	phpize
	patch -i $srcdir/freetds.patch -p0
	patch -i $srcdir/php-54.patch -p0
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$_pecl-$pkgver"

	make INSTALL_ROOT="$pkgdir" install
	echo ";extension=$_ext.so" > "$_ext.ini"
	install -Dm644 "$_ext.ini" "$pkgdir/etc/php/conf.d/$_ext.ini"
}

sha256sums=('604f0197a5c7b278a780fa1f7486bd04737e331ac0e8ebad183090fcba83d192'
            '24a3ee373ee7bdd516a39c5184512dfd01e62ac0116f456421660dfdb8b9067e'
            '5952b126577529b852d4376608af9ececcbb57aa8d80c02553a782ac11fbe9c5')
