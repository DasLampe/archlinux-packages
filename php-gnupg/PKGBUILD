# Contributor: Massimiliano Torromeo <massimiliano DOT torromeo AT google mail service>
pkgname=php-gnupg
pkgver=1.3.2
pkgrel=2
pkgdesc="PHP wrapper around the gpgme library."
arch=("i686" "x86_64")
url="http://pecl.php.net/package/gnupg"
license=('BSD')
depends=('php' 'gpgme' 'gnupg1')
source=("http://pecl.php.net/get/gnupg-${pkgver}.tgz" "gnupg-gpg1.patch")
backup=(etc/php/conf.d/gnupg.ini)

build() {
	cd "${srcdir}/gnupg-${pkgver}"
	patch -p1 -i "$srcdir/gnupg-gpg1.patch"

	phpize
	CFLAGS="-DGNUPG_PATH='\"/usr/bin/gpg1\"'" ./configure
	make
}

package() {
	cd "${srcdir}/gnupg-${pkgver}"	

	install -m0644 -D "LICENSE" "${pkgdir}/usr/share/licenses/php-gnupg/LICENSE"
	install -m0755 -d "${pkgdir}/etc/php/conf.d/"
	echo "extension=gnupg.so" > "${pkgdir}/etc/php/conf.d/gnupg.ini"
	chmod 0644 "${pkgdir}/etc/php/conf.d/gnupg.ini"
	install -m0755 -D ".libs/gnupg.so" "${pkgdir}$(php-config --extension-dir)/gnupg.so"
}

md5sums=('b55e594024dfbf101bc1db77b27cab52'
         '40eebd7d4e92a8d0df46bd6fdd578b82')
