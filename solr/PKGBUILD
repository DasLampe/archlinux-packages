# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=solr
pkgver=4.0.0
pkgrel=1
pkgdesc="Popular, blazing fast open source enterprise search platform from the Apache Lucene project"
arch=(any)
license=('Apache')
url="http://lucene.apache.org/solr/"
depends=(jetty java-environment)
makedepends=(unzip)
source=("http://www.apache.org/dist/lucene/solr/$pkgver/apache-solr-$pkgver.tgz" "jetty-env.xml")
install=solr.install
backup=(
	etc/solr/web.xml
	etc/solr/conf/admin-extra.html
	etc/solr/conf/scripts.conf
	etc/solr/conf/{elevate,schema,solrconfig}.xml
	etc/solr/conf/{mapping-FoldToASCII,mapping-ISOLatin1Accent,protwords,spellings,stopwords,synonyms}.txt
)

package() {
	cd "$srcdir/apache-solr-$pkgver"

	install -d "$pkgdir/etc/solr"
	install -d "$pkgdir/usr/share/solr"
	install -d "$pkgdir/usr/share/jetty/webapps"

	unzip "dist/apache-solr-$pkgver.war" -d "$pkgdir/usr/share/solr"
	rm -rf "$pkgdir/usr/share/solr/META-INF"

	cp -R example/solr/collection1/conf "$pkgdir/etc/solr"
	ln -s /etc/solr "$pkgdir/usr/share/solr/conf"

	mv "$pkgdir/usr/share/solr/WEB-INF/web.xml" "$pkgdir/etc/solr"
	ln -s /etc/solr/web.xml "$pkgdir/usr/share/solr/WEB-INF/web.xml"

	install -m0644 "$srcdir/jetty-env.xml" "$pkgdir/usr/share/solr/WEB-INF/jetty-env.xml"
	ln -s /usr/share/solr "$pkgdir/usr/share/jetty/webapps/solr"
}

sha256sums=('4b7ac4fdc1f1610f2d13e8dddf310e9d61620c00c463ba24e0f385a4c5c6f3ec'
            'f5e94950c8ba6d2c4815eb2e0f8d65908e3ee2649c714f19247ddfe536d6b073')
