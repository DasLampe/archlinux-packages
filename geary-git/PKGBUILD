# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=geary-git
pkgver=20120425
pkgrel=1
pkgdesc="A new lightweight, easy-to-use, feature-rich email client (pre-alpha version)"
arch=(i686 x86_64)
url="http://redmine.yorba.org/projects/geary"
license=('GPL')
depends=('sqlheavy-git' 'libunique3' 'gmime24' 'libwebkit3' 'libgnome-keyring' 'gtksourceview3' 'libgee' 'dconf')
makedepends=('vala-devel' 'git')
install=geary.install

_gitroot="git://yorba.org/geary"
_gitname="geary"

build() {
	cd "$srcdir"

	msg "Connecting to GIT server...."

	if [ -d $srcdir/$_gitname ]; then
		cd $_gitname && git pull origin
		msg "The local files are updated."
	else
		git clone --depth=0 $_gitroot $_gitname
	fi

	msg "GIT checkout done or server timeout"

	rm -rf "$srcdir/$_gitname-build"
	cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
	cd "$srcdir/$_gitname-build/"
	rm -rf .git .gitmodules
	find . -type f -name .gitignore -delete

	cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr .
	make
}

package() {
	cd "$srcdir/$_gitname-build"
	make install DESTDIR="$pkgdir"
}
