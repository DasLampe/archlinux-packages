# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Martin Erik Werner ("arand") <martinerikwerner@gmail.com>

pkgname=redeclipse
pkgver=1.3.1
pkgrel=1
pkgdesc="A single-player and multi-player first-person ego-shooter, built as a total conversion of Cube Engine 2"
arch=('i686' 'x86_64')
url="http://redeclipse.net"
license=('custom' 'CCPL' 'ZLIB' 'MIT')
depends=('sdl_image'  'libgl' 'sdl_mixer' 'hicolor-icon-theme')
makedepends=('mesa' 'imagemagick')
install=redeclipse.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}_${pkgver}_nix_bsd.tar.bz2)

build() {
	cd "$srcdir/$pkgname/src"
	make
}

package() {
	cd "$srcdir/$pkgname"

	make -C src DESTDIR="$pkgdir" prefix=/usr system-install

	chown -R root:games "$pkgdir/usr/"{share,lib}"/redeclipse"
	chmod -R o-rwx "$pkgdir/usr/lib/redeclipse"
	find "$pkgdir/usr/share/redeclipse" -type d -exec chmod 750 {} \;
	find "$pkgdir/usr/share/redeclipse" -type f -exec chmod 640 {} \;

	#install -Dm644 license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	#install -Dm644 trademark.txt "$pkgdir/usr/share/licenses/$pkgname/TRADEMARK"
}

sha256sums=('e29bc8a148c00b8a62b0e77752b7494f0d36f428cb4f24e9b5dc40cdcd67254e')
