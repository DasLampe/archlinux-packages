# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=redeclipse
pkgver=1.1
pkgrel=2
pkgdesc="A single-player and multi-player first-person ego-shooter, built as a total conversion of Cube Engine 2"
arch=('i686' 'x86_64')
url="http://redeclipse.net"
license=('custom' 'ZLIB')
depends=('sdl_image'  'libgl' 'sdl_mixer')
makedepends=('mesa' 'imagemagick')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}_${pkgver}_linux_bsd.tar.bz2 redeclipse.desktop)

md5sums=('e1330a858944e3bdb98fd15e07025d5f'
         'd905ff937b097fd946e9e5b3113aee3c')

build() {
	cd "$srcdir/$pkgname/src"
	make
}

package() {
	cd "$srcdir/$pkgname"

	install -d "$pkgdir/usr/"{share/$pkgname,}/bin
	sed -i "s|RE_DATA=.|RE_DATA=/usr/share/$pkgname|" $pkgname.sh
	install -m755 src/re{server,client} "$pkgdir/usr/share/$pkgname/bin/"
	cp -rf  data/ "$pkgdir/usr/share/$pkgname"

	cat <<EOF > $pkgdir/usr/bin/$pkgname
#!/bin/sh
cd /usr/share/$pkgname
exec bin/reclient -r "\$@"
EOF
	chmod 755 $pkgdir/usr/bin/$pkgname

	install -Dm644 "$srcdir/$pkgname.desktop"  $pkgdir/usr/share/applications/$pkgname.desktop
	install -d "$pkgdir/usr/share/icons/hicolor/48x48/apps/"
	convert -resize 48x48 'src/redeclipse.ico[2]' "$pkgdir/usr/share/icons/hicolor/48x48/apps/$pkgname.png"
	install -Dm644 license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
