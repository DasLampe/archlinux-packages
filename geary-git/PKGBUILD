# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=geary-git
pkgver=20120403
pkgrel=1
pkgdesc="A new lightweight, easy-to-use, feature-rich email client (pre-alpha version)"
arch=(i686 x86_64)
url="http://redmine.yorba.org/projects/geary"
license=('GPL')
depends=('sqlheavy-git' 'libunique3' 'gmime24' 'libwebkit3' 'libgnome-keyring' 'gtksourceview3' 'libgee' 'dconf')
makedepends=('vala-devel' 'git')
install=geary.install

_gitroot="git://yorba.org/geary"
_gitname="geary"

build() {
	cd "$srcdir"

	msg "Connecting to GIT server...."

	if [ -d $srcdir/$_gitname ]; then
		cd $_gitname && git pull origin
		msg "The local files are updated."
	else
		git clone --depth=0 $_gitroot $_gitname
	fi

	msg "GIT checkout done or server timeout"

	rm -rf "$srcdir/$_gitname-build"
	cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
	cd "$srcdir/$_gitname-build/"
	rm -rf .git .gitmodules
	find . -type f -name .gitignore -delete

	./waf configure build --prefix=/usr
}

package() {
	cd "$srcdir/$_gitname-build"

	./waf install --destdir="$pkgdir"

	install -dm0755 "$pkgdir/usr/share/geary"
	cp -a ui "$pkgdir/usr/share/geary"

	install -dm0755 "$pkgdir/usr/share/pixmaps"
	cp icons/* "$pkgdir/usr/share/pixmaps"

	mv "$pkgdir/usr/bin/geary" "$pkgdir/usr/bin/geary-bin"
	cat <<EOF > "$pkgdir/usr/bin/geary"
#!/bin/bash
cd /usr/share/geary
exec /usr/bin/geary-bin "\$@"
EOF
	chmod 755 "$pkgdir/usr/bin/geary"
}
