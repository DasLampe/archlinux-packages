# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=graylog2-server
pkgver=0.9.5p1
pkgrel=5
pkgdesc="Graylog2 is an open source syslog implementation that stores your logs in MongoDB"
arch=(any)
url="http://graylog2.org/"
license=('GPL')
depends=(java-runtime mongodb)
optdepends=(
	"zeromq"
	"apache-activemq"
	"rabbitmq"
)
backup=(etc/graylog2.conf etc/graylog2.d/rules/graylog2.drl etc/conf.d/graylog2)
source=(
	https://github.com/downloads/Graylog2/$pkgname/$pkgname-$pkgver.tar.gz
	https://github.com/Graylog2/graylog2-server/raw/aa3ab41671cea39545b499f03b2c454546e3f0de/misc/graylog2.drl
	graylog2.rc.d
	graylog2.conf.d
)

package() {
	cd "$srcdir/$pkgname-$pkgver"

	install -D -m0755 "$srcdir/graylog2.rc.d" "$pkgdir/etc/rc.d/graylog2"
	install -D -m0755 "$srcdir/graylog2.conf.d" "$pkgdir/etc/conf.d/graylog2"
	install -D -m0644 "$srcdir/graylog2.drl" "$pkgdir/etc/graylog2.d/rules/graylog2.drl"
	install -D -m0644 graylog2-server.jar "$pkgdir/usr/lib/graylog2-server.jar"

	sed -e 's|mongodb_host.*|mongodb_host = 127.0.0.1|' \
		-e 's|mongodb_useauth.*|mongodb_useauth = false|' \
		graylog2.conf.example > "$pkgdir/etc/graylog2.conf"
}

md5sums=('4b68488551c08bd80839a727319b1c98'
         '6e67d9c7b2d8bf648d7a76740493fcb7'
         'ccebd3eb840a3cdc1569630a84239297'
         '89aeea373b25af3ebb38848c5771e10a')
