# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=graylog2-web-interface
pkgver=0.11.0
pkgrel=1
pkgdesc="Graylog2 is an open source syslog implementation that stores your logs in MongoDB (web interface)"
arch=(any)
url="http://graylog2.org"
license=('GPL')
depends=(ruby-bundler)
source=(
	http://download.graylog2.org/$pkgname/$pkgname-$pkgver.tar.gz
	dispatcher.rb mongoid.yml graylog2.cron)
install=graylog2.install
backup=(etc/webapps/graylog2/{email,general,mongoid}.yml)
options=(!strip)

build() {
	cd "$srcdir/$pkgname-$pkgver"
	bundle install --deployment --binstubs --without test development
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	install -d -m0755 "$pkgdir/usr/share/webapps/graylog2"
	install -d -m0755 "$pkgdir/etc/webapps/graylog2"

	cp -a * "$pkgdir/usr/share/webapps/graylog2"

	for _CONF in {general,mongoid,indexer,ldap}.yml; do
		mv "$pkgdir/usr/share/webapps/graylog2/config/$_CONF" "$pkgdir/etc/webapps/graylog2/$_CONF"
		ln -s "/etc/webapps/graylog2/$_CONF" "$pkgdir/usr/share/webapps/graylog2/config/$_CONF"
	done

	cp "$srcdir/mongoid.yml" "$pkgdir/etc/webapps/graylog2/mongoid.yml"

	install -D -m0755 "$srcdir/graylog2.cron" "$pkgdir/etc/cron.d/graylog2"
	install -D -m0755 "$srcdir/dispatcher.rb" "$pkgdir/usr/share/webapps/graylog2/public/dispatcher.rb"
}

sha256sums=('082e760a123344594088bb5550bdff949ce51e40ca805ef2937e7fb1180e4670'
            '289b1edbd11bfac53cb3b2fdeb7d95bd6075fbaa727f9391a9985027ed456603'
            'ff8680559fc69a74b84ce8d353b4474fa6085d725faeb85a846af13a39a51ca3'
            'a2b8413adbb12dcf4ae2744b183a51dd477e2873d337f274e24bfea6422be05d')
