# Maintainer: Jonathan Liu <net147@gmail.com>

pkgname=util-vserver
pkgver=0.30.216_pre3025
_realpkgver=${pkgver/_/-}
pkgrel=1
pkgdesc="Provides tools for kernels with the security context patch"
arch=('i686' 'x86_64')
url="http://savannah.nongnu.org/projects/util-vserver/"
license=('GPL2')
depends=('nss' 'python2')
makedepends=('autoconf' 'automake' 'iproute2' 'iptables' 'vconfig')

source=("http://people.linux-vserver.org/~dhozac/t/uv-testing/${pkgname}-${_realpkgver}.tar.bz2"
        "python2.patch"
        "vserver-build-pacman.patch"
        "vserver-build.rpm.patch")

build() {
	cd "${srcdir}/${pkgname}-${_realpkgver}"
	patch -Np1 -i "${srcdir}/python2.patch"
	patch -Np1 -i "${srcdir}/vserver-build-pacman.patch"
	patch -Np1 -i "${srcdir}/vserver-build.rpm.patch"
	msg "Running autoreconf..."
	autoreconf -fi || true
	msg "Continuing build()..."
	PYTHON=python2 ./configure --prefix=/usr --sysconfdir=/etc --with-initrddir=/etc/rc.d --localstatedir=/var --disable-dietlibc --with-crypto-api=nss
	make
}

package() {
	cd "${srcdir}/${pkgname}-${_realpkgver}"
	make DESTDIR="${pkgdir}" install
	make DESTDIR="${pkgdir}" install-distribution
	# Move the v_* scripts out of the init dir, as Arch does not support SYSV style init scripts
	install -m755 -d "${pkgdir}/usr/libexec/vserver"
	mv "${pkgdir}/etc/rc.d"/v_* "${pkgdir}/usr/libexec/vserver"
}

md5sums=('46592e4695c6011c828872b66a483fc0'
         '7f32376706c566a80648950a71f3aceb'
         'a84c99744aec850cf3eee9549af22c62'
         'e7453251105ab2705783723f0ec3b8a1')
