# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libuv
pkgver=0.9.8
pkgrel=1
pkgdesc="libuv is a new platform layer for Node.JS"
arch=('i686' 'x86_64')
url="https://github.com/joyent/libuv"
license=('custom')
depends=('glibc')
source=("$pkgname-$pkgver.tar.gz::https://github.com/joyent/libuv/tarball/node-v$pkgver")

build() {
	cd "$srcdir"
	_tardir=$(tar -tzf $pkgname-$pkgver.tar.gz --exclude "*/*" | head -n 1)
	cd "$_tardir"

	CFLAGS=-fPIC make
}

check() {
	cd "$srcdir"
	_tardir=$(tar -tzf $pkgname-$pkgver.tar.gz --exclude "*/*" | head -n 1)
	cd "$_tardir"

	make test
}

package() {
	cd "$srcdir"
	_tardir=$(tar -tzf $pkgname-$pkgver.tar.gz --exclude "*/*" | head -n 1)
	cd "$_tardir"

	install -Dm644 LICENSE \
		"$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 AUTHORS \
		"$pkgdir/usr/share/doc/$pkgname/AUTHORS"
	install -Dm644 README.md \
		"$pkgdir/usr/share/doc/$pkgname/README.md"

	install -Dm644 libuv.a "$pkgdir/usr/lib/libuv.a"
	install -Dm644 libuv.so "$pkgdir/usr/lib/libuv.so"

	cd include
	find -type f -exec \
		install -Dm644 {} "$pkgdir/usr/include/$pkgname/"{} \;
}

sha256sums=('bf312dbb1d08d93cc556af7636512e1fcc0e43b6900df3c7f7f43d2002c38d27')
