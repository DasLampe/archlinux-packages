# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=mydumper
pkgver=0.5.1
pkgrel=2
pkgdesc="A high performance MySQL backup tool."
arch=("i686" "x86_64")
url="http://www.mydumper.org"
license=('GPL')
depends=("glib2" "pcre" "libmysqlclient")
makedepends=("cmake" "python2-sphinx")

source=("http://launchpad.net/mydumper/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz"
        0005-zlib-file-type-change-fixes.patch)

build() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -p1 -i "$srcdir/0005-zlib-file-type-change-fixes.patch"
	sed -i 's/sphinx-build/sphinx-build2/' cmake/modules/FindSphinx.cmake
	cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
}

md5sums=('b4df42dfe23f308ab13eb6ecb73a0d21'
         '85e9063cbd4cfa270a5083d17dc6bed8')
sha256sums=('2ae041248c8dea0f7c3e2ca2b7322d74186c9d0e1626787a864d1eb0cb867b15'
            'd134cc085ff03e439d0df1f9a8e335e6a1f7a9c490de11c2a0140a8ac86794be')
