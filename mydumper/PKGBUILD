# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=mydumper
pkgver=0.5.2
pkgrel=1
pkgdesc="A high performance MySQL backup tool."
arch=("i686" "x86_64")
url="http://www.mydumper.org"
license=('GPL')
depends=("glib2" "pcre" "libmysqlclient")
makedepends=("cmake")
# makedepends=("cmake" "python2-sphinx")

source=("http://launchpad.net/mydumper/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz")

build() {
	cd "$srcdir/$pkgname-$pkgver"
	sed -i 's/sphinx-build/sphinx-build2/' cmake/modules/FindSphinx.cmake
	cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DBUILD_DOCS=OFF
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
}

sha256sums=('a7f780f030036eb360488613a220029d59a2e20733641cc9e24ef6f3431e88cd')
