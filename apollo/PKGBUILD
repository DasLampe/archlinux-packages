# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=apollo
pkgver=1.2
pkgrel=1
pkgdesc="A faster, more reliable, easier to maintain messaging broker built from ActiveMQ"
arch=(any)
url="http://activemq.apache.org/apollo/"
license=('Apache 2.0')
depends=('java-runtime')
makedepends=('maven')
backup=(etc/conf.d/apollo)
source=("http://www.eu.apache.org/dist/activemq/activemq-$pkgname/$pkgver/$pkgname-project-$pkgver-source-release.tar.gz" apollo-rc apollo-bin apollo-create-bin apollo-run-bin)

build() {
	cd "$srcdir/$pkgname-project-$pkgver"

	cat << EOF > "$srcdir/maven.xml"
<settings>
	<localRepository>$srcdir/mvn-repo</localRepository>
</settings>
EOF

	MAVEN_OPTS="-Xmx512m -XX:MaxPermSize=128m" mvn -s "$srcdir/maven.xml" -Dtest=false install
}

package() {
	cd "$pkgdir"

	tar --strip-components=1 -xzf "$srcdir/$pkgname-project-$pkgver/apollo-distro/target/apache-$pkgname-$pkgver-unix-distro.tar.gz"

	# etc
	install -Dm0755 "$srcdir/apollo-rc" "$pkgdir/etc/rc.d/apollo"
	install -dm0755 etc/$pkgname
	install -dm0755 etc/conf.d
	mv etc/jul.properties etc/$pkgname/
	echo -e "# The name of the broker to run. You can create on with \"apollo-create BROKERNAME\"\nBROKER=\"\"" > etc/conf.d/apollo

	# bin
	install -Dm0755 "$srcdir/apollo-bin" usr/bin/apollo
	install -Dm0755 "$srcdir/apollo-create-bin" usr/bin/apollo-create
	install -Dm0755 "$srcdir/apollo-run-bin" usr/bin/apollo-run
	rm -rf bin

	# lib
	install -dm0755 usr/lib
	mv lib usr/lib/$pkgname

	# docs
	install -dm0755 usr/share/doc/$pkgname
	mv docs/site/* examples NOTICE readme.html usr/share/doc/$pkgname/
	rm -rf docs
	rm LICENSE
}

md5sums=('57c465b8f8050e0b22b5340566cf6d57'
         'e8aa4b456df8e8205da47dce7a1ebdb5'
         '47521c761ef39069ee51d2304f56c5b3'
         '37688fbcf9f1f048d651eaf4158dc55b'
         '9232fa90ed2ac006c7a6fcdf076cc2b7')
