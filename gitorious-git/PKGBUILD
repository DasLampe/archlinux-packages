# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=gitorious-git
pkgver=20120809
pkgrel=1
pkgdesc="Gitorious aims to provide a great way of doing distributed opensource code collaboration (git version)"
arch=(i686 x86_64)
url="http://gitorious.org/gitorious"
license=('AGPLv3')

depends=('sphinx' 'mysql' 'ruby-bundler' 'imagemagick' 'nodejs-buster' 'libxslt')
makedepends=('git')
optdepends=(
	'memcached: High performance cache'
	'redis: message queue')
options=('!strip')

install=gitorious.install
backup=(
	etc/webapps/gitorious/gitorious.yml
	etc/webapps/gitorious/database.yml
	etc/webapps/gitorious/authentication.yml
	etc/webapps/gitorious/memcache.yml
	etc/webapps/gitorious/resque.yml
	etc/webapps/gitorious/unicorn.rb
)

source=(
	gitorious-daemon.rc.d
	gitorious-resque-worker.rc.d
	sphinx-cmd.patch
)

conflicts=(gitorious)
provides=(gitorious)

_gitroot="git://gitorious.org/gitorious/mainline.git"
_gitname="gitorious"

build() {
	cd "$srcdir"

	# trick systemtimer build system into thinking we have gcc-4.6
	rm -rf gcc46-pathfix
	mkdir gcc46-pathfix
	ln -s /usr/bin/gcc gcc46-pathfix/gcc-4.6
	export PATH="$PATH:$srcdir/gcc46-pathfix"

	msg2 "Connecting to GIT server...."

	if [ -d "$srcdir/$_gitname" ]; then
		cd $_gitname && git pull origin
		msg "The local files are updated."
	else
		git clone -b feature/rails3 $_gitroot $_gitname
		cd $_gitname
	fi
	git submodule update --recursive --init

	msg2 "GIT checkout done or server timeout"

	rm -rf "$srcdir/$_gitname-build"
	cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
	cd "$srcdir/$_gitname-build/"
	find . -type f -name .git\* -delete
	find . -type d -name .git\* -exec rm -rf {} +

	msg2 "Fetching bundled gems..."
	bundle install --deployment --binstubs
	rm -rf vendor/bundle/ruby/*/cache

	msg2 "Patching application..."
	# Activate production mode
	sed -i "s|^# ENV\['RAILS_ENV'\]|ENV['RAILS_ENV']|" config/environment.rb

	patch -p1 -i "$srcdir/sphinx-cmd.patch"
}

package() {
	cd "$srcdir"

	_gitorious="/usr/share/webapps/gitorious"
	_etc="$pkgdir/etc/webapps/gitorious"
	install -d "$pkgdir/usr/share/webapps"
	install -d "$_etc"

	msg2 "Copying gitorious files"
	cp -a "$_gitname-build" "${pkgdir}${_gitorious}"

	# Configurations
	msg2 "Moving configuration files"
	for f in "$pkgdir$_gitorious/config/"*.sample*; do
		fdest=$(basename "$f")
		fdest=${fdest/.sample/}
		install -Dm0644 "$f" "$_etc/$fdest"
		rm "$f"
		ln -s "/etc/webapps/gitorious/$fdest" "$pkgdir$_gitorious/config/$fdest"
	done

	sed -e 's|^  password:\s*$|  password: G170r!oUs|' \
	    -e 's|^  username:.*$|  username: gitorious|' \
	    -e 's|gitorious_production|gitorious|' \
	    -i "$_etc/database.yml"

	sed -e 's|/var/git/|/var/gitorious/|' \
	    -e 's|gitorious.local|localhost.localdomain|' \
	    -i "$_etc/gitorious.yml"

	# capillary
	ln -s /usr/lib/node_modules/buster/node_modules/ "${pkgdir}${_gitorious}/public/javascripts/lib/capillary/node_modules"

	# services
	msg2 "Installing services"
	install -Dm0755 "$srcdir/gitorious-daemon.rc.d" "$pkgdir/etc/rc.d/gitorious-daemon"
	install -Dm0755 "$srcdir/gitorious-resque-worker.rc.d" "$pkgdir/etc/rc.d/gitorious-resque-worker"

	# data directories
	msg2 "Setting up data directories"
	install -dm0755 "$pkgdir/var/gitorious/"{tarballs,tarballs-work,repositories}

	# remove tests
	msg2 "Removing tests"
	find "$pkgdir" -type d \( -name test -o -name tests \) -exec rm -rf {} +
}

sha256sums=('040fdf9c9f9e39f5b0a8483befce74ed47750d813be421dd653393205f20b5c8'
            '0b48f79cbd64bf01bd6bad02c30ef51391040114479e24b772dceef91a60c19c'
            '65be83bccf7d18ebe87efb2ab318cea3e9e79ac36fa16e3217b5ae2b92963f7e')
