# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
pkgname=gitorious-git
pkgver=20110324
pkgrel=1
pkgdesc="Gitorious aims to provide a great way of doing distributed opensource code collaboration."
arch=(i686 x86_64)
url="http://gitorious.org/gitorious"
license=('AGPLv3')
depends=('apache-activemq' 'sphinx' 'mysql' 'ruby-enterprise' 'ruby-enterprise-bundler' 'imagemagick')
makedepends=('git')
optdepends=('memcached: High performance cache')
source=(gitorious.rc.d gitorious-ultrasphinx.rc.d database.yml gitorious.yml sphinx-cmd.patch)
install=gitorious.install
backup=(etc/webapps/gitorious/database.yml etc/webapps/gitorious/gitorious.yml)

_gitroot="git://gitorious.org/gitorious/mainline.git"
_gitname="gitorious"

build() {
	cd $srcdir
	msg "Connecting to GIT server...."

	if [ -d $srcdir/$_gitname ]; then
		cd $_gitname && git pull origin
		msg "The local files are updated."
	else
		git clone $_gitroot $_gitname
	fi

	msg "GIT checkout done or server timeout"

	rm -rf $srcdir/$_gitname-build
	cp -r $srcdir/$_gitname $srcdir/$_gitname-build
	cd $srcdir/$_gitname-build/
	rm -rf .git .gitmodules
	find . -type f -name .gitignore -delete

	msg "Fetching bundled gems..."
	/opt/ruby-enterprise/bin/bundle install --deployment --binstubs
	rm -rf vendor/bundle/ruby/*/cache

	msg "Patching application..."
	# Activate production mode
	sed -i "s|# ENV\['RAILS_ENV'\]|ENV['RAILS_ENV']|" config/environment.rb

	# Disable SSL
	sed -i "/^# Settings specified here/ a\
SslRequirement.disable_ssl_check = true
" config/environments/production.rb

	sed -i "/^require 'rubygems'/ a\
ENV['BUNDLER_GEMFILE'] = File.dirname(__FILE__)+'/../../Gemfile'\n\
require 'bundler/setup'
" data/hooks/messaging.rb

	patch -p1 -i "$srcdir/sphinx-cmd.patch"

	find bin script data/hooks -type f -exec sed -i 's|^#!/usr/bin/env ruby|#!/opt/ruby-enterprise/bin/ruby|' {} \;
}

package() {
	cd $srcdir

	_gitorious="/usr/share/webapps/gitorious"
	_etc="$pkgdir/etc/webapps/gitorious"
	install -d "$pkgdir/usr/share/webapps"
	install -d "$_etc"

	mv $_gitname-build "${pkgdir}${_gitorious}"

	install -D -m0644 "$srcdir/database.yml" "$_etc/database.yml"
	install -D -m0644 "$srcdir/gitorious.yml" "$_etc/gitorious.yml"
	install -D -m0644 "${pkgdir}${_gitorious}/config/broker.yml.example" "${pkgdir}${_gitorious}/config/broker.yml"

	ln -s "/etc/webapps/gitorious/database.yml" "${pkgdir}${_gitorious}/config/"
	ln -s "/etc/webapps/gitorious/gitorious.yml" "${pkgdir}${_gitorious}/config/"

	install -d "$pkgdir/usr/bin"
	ln -s "${_gitorious}/script/gitorious" "$pkgdir/usr/bin/gitorious"

	install -D -m0755 "$srcdir/gitorious.rc.d" "$pkgdir/etc/rc.d/gitorious"
	install -D -m0755 "$srcdir/gitorious-ultrasphinx.rc.d" "$pkgdir/etc/rc.d/gitorious-ultrasphinx"
}

md5sums=('e056bf33a122d47b62dab059e6e75882'
         'd6a94e85d1efe41b4b3aba9b8263702e'
         '6a6efd4a0c9deaaefe841c57176a1bee'
         'c1b329f5e923b31b979dc96300f5261a'
         'f46e84a467b492a6d5de936e1595e028')
