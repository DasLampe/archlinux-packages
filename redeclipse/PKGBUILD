# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Martin Erik Werner ("arand") <martinerikwerner@gmail.com>

pkgname=redeclipse
pkgver=1.2
pkgrel=3
pkgdesc="A single-player and multi-player first-person ego-shooter, built as a total conversion of Cube Engine 2"
arch=('i686' 'x86_64')
url="http://redeclipse.net"
license=('custom' 'CCPL' 'ZLIB' 'MIT')
depends=('sdl_image'  'libgl' 'sdl_mixer' 'hicolor-icon-theme')
makedepends=('mesa' 'imagemagick')
install=redeclipse.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}_${pkgver}_linux_bsd.tar.bz2 security-text-command-fix.patch)

build() {
	cd "$srcdir/$pkgname/src"
	patch -p2 -i "$srcdir/security-text-command-fix.patch"
	make
}

package() {
	cd "$srcdir/$pkgname"

	make -C src DESTDIR="$pkgdir" prefix=/usr system-install

	chown -R root:games "$pkgdir/usr/"{share,lib}"/redeclipse"
	chmod -R o-rwx "$pkgdir/usr/lib/redeclipse"
	find "$pkgdir/usr/share/redeclipse" -type d -exec chmod 750 {} \;
	find "$pkgdir/usr/share/redeclipse" -type f -exec chmod 640 {} \;

	install -Dm644 license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 trademark.txt "$pkgdir/usr/share/licenses/$pkgname/TRADEMARK"
}

md5sums=('e133361f79be01782bf0bbc4c5a01236'
         '89253e190972021c9e89b75d91ee408b')
